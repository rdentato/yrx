!   Convert man page to HTML
!
! $Id: man-html.dat,v 1.3 2003/06/24 20:25:12 gray Exp $

\B=\<\!-- "@outpath" generated @just-date{@datime} by "@getenv{USER;}" --\>\n\
   \<\!-- Converted from "@inpath" dated @just-date{@file-time} --\>\n

just-date:<L> <L> <D> <D>\:<D>\:<D> <D>=$2 $3\, $7@end
just-date:\n=;

\<=\&lt\;
\>=\&gt\;
\&=\&amp\;

\N.\\\"*\n=\<\!-- * --\>\n
\\\"*\n=\n\<\!-- * --\>\n
\N.\W\n=\n
\N.TH\L <arg> <arg> <arg>*<endline>=\
	\<TITLE\>@downcase{$1}($2)\</TITLE\>\n\
	\<\!-- $3 $4 --\>\n\<H1\>$1\</H1\>\n
\N.TH\L <arg> <arg>\W<endline>=\
	\<TITLE\>@downcase{$1}($2)\</TITLE\>\n\<H1\>$1\</H1\>\n
!\N.SH <arg> =@endlist{$P}\N\<H2\>$1\</H2\>\n
\N.SH\L "*"=@endlist{$P}\N\<H2\>@{$1}\</H2\>\n
\N.SH\L *\n=@endlist{$P}\N\<H2\>@{$1}\</H2\>\n
\N.SS <arg> =@endlist{$P}\N\<H3\>$1\</H3\>\n

endline:\W\n=@end
endline:^M^J=@end
endline:\\\"*\n=@end
endline:\s=;\t=;
endline:?=@fail
^M^J=\n

@set{font;}
startfont:*=\<$1\>@push{font;$1}
endfont:<L>=\</$1\>@pop{font}

\N.I\L <optarg>= @infont{I,$1} ;
\N.B\L <optarg>= @infont{TT,$1} ;
\N.I\n*\n= @infont{I,$1} ;
\N.B\n*\n= @infont{TT,$1} ;
infont:<L>,<U>=@startfont{$1}@{$2}@endfont{${font}};=@end
\\fI=@startfont{I}
\\fB=@startfont{TT}
\\fP=@endfont{@var{font;}}
\\fR=@repeat{5;@endfont{@var{font;}}}
\\f1=@endfont{@var{font;}}
\\f2=@startfont{I}
\\f3=@startfont{TT}
\\fL=@startfont{TT}

\N.ft B =@startfont{B}
\N.ft I =@startfont{I}
\N.ft P =@endfont{@var{font;}}
\N.ft R =@endfont{@var{font;}}
\N.ft 1 =@endfont{@var{font;}}
\N.ft 2 =@startfont{I}
\N.ft 3 =@startfont{B}
\N.ft L =@startfont{TT}
\N.ft\W\n=@endfont{@var{font;}}

\\fB<I>\W(<A1>)\\fP=@wrap{\<A HREF="$1.$2.html"\>\<TT\>$1\</TT\>($2)\</A\>}
\N.BR <I>\W(<A1>)=@wrap{\<A HREF="$1.$2.html"\>\<TT\>$1\</TT\>($2)\</A\>}
\N.BR <I> ( <A1> )=@wrap{\<A HREF="$1.$2.html"\>\
		\<TT\>$1\</TT\>($2)\</A\>}

\N.BR \L<optarg><optarg><optarg><optarg><optarg><optarg><optarg><optarg>\N= \
  @infont{TT,$1}@{$2}@infont{TT,$3}@{$4}@infont{TT,$5}@{$6}@infont{TT,$7}@{$8} ;
\N.IR \L<optarg><optarg><optarg><optarg><optarg><optarg>\N= \
  @infont{I,$1}@{$2}@infont{I,$3}@{$4}@infont{I,$5}@{$6} ;

\N.BI <optarg><optarg><optarg><optarg><optarg><optarg><optarg><optarg>\N=\
	@infont{TT,$1}@infont{I,$2}@infont{TT,$3}@infont{I,$4}\
	@infont{TT,$5}@infont{I,$6}@infont{TT,$7}@infont{I,$8} ;
\N.IB <optarg><optarg><optarg><optarg><optarg><optarg><optarg><optarg>\N=\
	@infont{I,$1}@infont{TT,$2}@infont{I,$3}@infont{TT,$4}\
	@infont{I,$5}@infont{TT,$6}@infont{I,$7}@infont{TT,$8} ;
\N.RB <optarg><optarg><optarg><optarg><optarg><optarg>\N= @{$1}@infont{TT,$2}\
	@{$3}@infont{TT,$4}@{$5}@infont{TT,$6} ;
\N.RI <optarg><optarg><optarg><optarg><optarg><optarg><optarg><optarg>\N=\
	@{$1}@infont{I,$2}@{$3}@infont{I,$4}\
	@{$5}@infont{I,$6}@{$7}@infont{I,$8} ;

optarg:"\L<argbody>=$1@end
optarg:\N=@end
optarg:\L\S=@end
optarg:\\c\n=
optarg:\\\ =\\\ ;
optarg:\\\\=\\;
argbody:""="
argbody:"\L\W=@end
argbody:\\\n=
argbody:\N=@end
argbody:\\\\=\\;

\N.SB \L#\N= \<TT\>#\</TT\> ;
\N.PP =@newpar{}
newpar:\Z=@endlist{$P}\<P\>\n
\N.LP =@newpar{}
\N.SP =@newpar{}
\N.sp<S0>\L*\n=\<P\>\n
\n\W\n=@newpar{}
\n=\N
\n.br<endline>=\<BR\>\n
\N.br =\<BR\>
\N.IX\L *\n=
\N.na<endline>=
\N.nh<endline>=
\N.hy<endline>=
\N.hy\L <D><endline>=
\N.hw\L *\n=
\N.ne\I*\n=
!\N.nf<endline>#\n.fi<endline>=\<PRE\>\n#\n\</PRE\>\n
\N.nf<endline>=\<PRE\>\n@set{EndPre;\</PRE\>\n}
\N.fi<endline>=${EndPre;}@set{EndPre;}
! Note: the ALIGN attribute is only in level 3 (HTML+)
\N.ce<endline>#\n=\<P ALIGN\=\"CENTER\"\>\n#\n\<P\>\n
\N.ce *\n#\n.ce 0\W\n=\<P ALIGN\=\"CENTER\"\>\n#\n\<P\>\n
\N.bp<endline>=\<P\>\n

\N.nr <I> <G>\n=@define{\\\\n$1\\I\=\@\{@quote{$2}\}}\
		@define{\\\\n($1\\I\=\@\{@quote{$2}\}}
\N.ad /[bnl]/\W\n=

\N.eo<endline>*\n.ec<endline>=@litesc{*}
litesc:\\=\\
litesc::

\N.TP\L\W<a>\W\n\W\\(bu\W=@startlist{UL}\<LI\>
\N.TP\L\W<a>\W\n#\N=@startlist{DL}\<DT\>$2\n\<DD\>
\N.RS\I*\n=@push{P;\t}
\N.RE<endline>=@endlist{@var{P;}}@endRS{@var{P;}}
\N.IP\L \\(bu=@startlist{UL}\<LI\>
\N.IP<endline>=\<P\>\n
\N.IP\L <U>\n=@startlist{DL}\<DT\>@{$1}\<DD\>

\N.PD\L\W<d><endline>=@ignored{.PD $1}
ignored:*=@err{"@file" line @line\: ignored\: *\n}
cut:<U20>* =$1$2...@end
cut:<U30>=$1...@end
\N.ll\L\W<n><endline>=@ignored{.ll $1}

@set{P;\f}
startlist:$P=;<L>=\<$1\>\n@push{P;$1}
endlist:<L>=\</$1\>@pop{P}@endlist{@var{P;\n}}
endlist:\S=
endRS:\t=@pop{P};?=

\N.in +<A><endline>#\N.in -<A><endline>=\N\<BLOCKQUOTE\>\n#\N\</BLOCKQUOTE\>\n

\\s-<D1>#\\s0=\<SMALL\>#\<\/SMALL\>
\\s+<D1>#\\s0=\<BIG\>#\<\/BIG\>
\N.SM <arg>\\s0=$1
\N.SM =

\N.ss\L <d><endline>=
\N.cs\L <d>\W<d>\W<d><endline>=

\N.if t .ti *\n=
\N.if n .ti *\n=\<BR\>\n
\N.if t .ig <G>\n*\n.$1\W\n=
\N.if n .ig <G>\n*\n.$1\W\n=@{*\n}
\N.if t *\n=@{*\n}
\N.if n *\n=

\N.de <G>\s<G><endline>*\n.$2 *\n=@define{\\N.@quote{$1} \<optarg\>\<optarg\>\
	\<optarg\>\<optarg\>\<optarg\>\<optarg\>\=\@\{@macbody{$4\N}\}}\
	@{.$2 $5\n}
\N.de <G><endline>*\n..=@define{\\N.@quote{$1} \<optarg\>\<optarg\>\
	\<optarg\>\<optarg\>\<optarg\>\<optarg\>\<optarg\>\<optarg\>=\
	\@\{@macbody{$3\N}\}}
\N.rm <G><endline> =@undefine{\\N.@quote{$1} \<optarg\>\<optarg\>\
	\<optarg\>\<optarg\>\<optarg\>\<optarg\>}

macbody:\\\\\$<D1>=\$$1
macbody:\\\\=\\\\
macbody:<A>=$1
macbody:?=@quote{$1}

\N.ds <G1> <stringval>=@define{@quote{\\\*$1}=@quote{@{$2}}}
\N.ds <G2> <stringval>=@define{@quote{\\\*\($1}=@quote{@{$2}}}
stringval:\n=@end;\\\n=;

\N.so <G><endline>=@{@read{@mergepath{@inpath;$1;$1}}}
\N.ex<endline>=@end

\N.*<endline>=@out-error{\N.$1}\N

! Emacs w3 recognizes these character names, but not Mosaic or Netscape.
!\\-=\&ndash\;
\\-=-;
!\\(em=\&mdash\;
\\(em=--
!\\(hy=\&hyphen\;
\\(hy=-
!\\\ =\&ensp\;
!\\\ =\&nbsp\;
\\\ =\ ;
!\\0=\&ensp\;
\\0=\ ;
!\\(bu=\&middot\;
\\(bu=\*
!\\(14=\&frac14\;
\\(14=\I1/4
!\\(12=\&frac12\;
\\(12=\I1/2
!\\(34=\&frac34\;
\\(34=\I3/4
\\(bv=\<B\>\|\</B\>
\\(pl=+
\\(mi=-
\\(eq=\=
\\(\>\==\>\=
\\(\<\==\&lt\;\=
\\(-\>=-\&gt\;
\\(\<-=\&lt\;-

\\\'=\'
\\\`=\`
\\\.=\.
\\\\=\\
\\e=\\
\\t=\t
\\\n=\n
\\\|=
\\\^=
\\\&=
\\c\n=
\\(co=\&copy\;
\\(rg=\&reg\;
\\(tm=\&trade\;
\\(sc=\&sect\;
\\(ru=_
\\(ul=_
\\\*R=\&reg\;
\\\*(lq=\`\`
\\\*(rq=\'\'
\\\[=\[
\\\]=\]

\\f?=@out-error{$0}
\\(??=@out-error{$0}
\\?\'<p40>\'=@out-error{$0}
\\?=@out-error{$0}
out-error:*=\<font color\=red\>$1\<\/font\>\
	@err{"@file" line @line\: Unrecognized\:  @cut{$1}\n}

arg:"\L<>"=$1@end
arg:<S0>=@terminate
arg:\\c\N=@terminate
arg::
args:\N=@end;\S=\S;"<>"=$1;
args:\\c\n=@terminate
args::

\Ihttp\:\/\/<host>=\<a href\="$0"\>$0\<\/a\>
\Iftp\:\/\/<host>=\<a href\="$0"\>$0\<\/a\>
host:\P. =@terminate;<I>=$1;\/=\/;\#=\#;\.<F0>=\.;<F1>=$1;=@terminate
host:\P\, =@terminate;\P\; =@terminate


